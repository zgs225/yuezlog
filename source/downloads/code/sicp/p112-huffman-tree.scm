;; Huffman tree

(define (make-leaf symbol weight)
  (list 'leaf symbol weight))

(define (leaf? object)
  (eq? (car object) 'leaf))

(define (symbol-leaf x) (cadr x))

(define (weight-leaf x) (caddr x))

(define (make-code-tree left right)
  (list left
        right
        (append (symbols left) (symbols right))
        (+ (weight left) (weight right))))

(define (left-branch tree) (car tree))

(define (right-branch tree) (cadr tree))

(define (symbols tree)
  (if (leaf? tree)
      (list (symbol-leaf tree))
      (caddr tree)))

(define (weight tree)
  (if (leaf? tree)
      (weight-leaf tree)
      (cadddr tree)))

(define (decode bits tree)
  (define (decode-1 bits current-branch)
    (if (null? bits)
        '()
        (let ((next-branch
                (choose-branch (car bits) current-branch)))
          (if (leaf? next-branch)
              (cons (symbol-leaf next-branch)
                    (decode-1 (cdr bits) tree))
              (decode-1 (cdr bits) next-branch)))))
  (decode-1 bits tree))

(define (choose-branch bit branch)
  (cond ((= bit 0) (left-branch branch))
        ((= bit 1) (right-branch branch))
        (else (error "bad bit -- CHOOSE-BRANCH" bit))))

(define (adjoin-set x set)
  (cond ((null? set) (list x))
        ((< (weight x) (weight (car set))) (cons x set))
        (else (cons (car set)
                    (adjoin-set x (cdr set))))))

(define (make-leaf-set pairs)
  (if (null? pairs)
      '()
      (let ((pair (car pairs)))
        (adjoin-set (make-leaf (car pair)
                               (cadr pair))
                    (make-leaf-set (cdr pairs))))))

(define (encode message tree)
  (if (null? message)
      '()
      (append (encode-symbol (car message) tree)
              (encode (cdr message) tree))))

(define (encode-symbol symbol tree)
  (define (concat-bit lst ele)
    (reverse (cons ele (reverse lst))))

  (define (in-branch? char tree)
    (define (iter char symbol-set)
      (cond ((null? symbol-set) #f)
            ((eq? char (car symbol-set)) #t)
            (else (iter char (cdr symbol-set)))))
    (iter char (symbols tree)))

  (define (encode-action bits symbol current-tree)
    (cond ((leaf? current-tree) bits)
          ((in-branch? symbol (left-branch current-tree))
           (encode-action (concat-bit bits 0)
                          symbol
                          (left-branch current-tree)))
          ((in-branch? symbol (right-branch current-tree))
           (encode-action (concat-bit bits 1)
                          symbol
                          (right-branch current-tree)))
          (else (error "Char" symbol "not in encoding dict."))))
  (encode-action '() symbol tree))
